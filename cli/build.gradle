plugins {
    id 'java-library-distribution'
    id "com.palantir.docker" version "0.33.0"
    id "org.unbroken-dome.test-sets" version "4.0.0"
    id 'java'
}

dependencies {
    implementation project(':codegen-utils')
    implementation project(':model-codegen')
    implementation project(':jersey-codegen')
    implementation project(':spring-codegen')
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8'
    implementation 'com.fern.fern:ir-v11-model'
    implementation 'org.immutables:value::annotations'
    implementation 'com.squareup:javapoet'
    implementation 'ch.qos.logback:logback-classic'
    implementation 'com.fern-fern.fern:generator-logging-api-client'

    annotationProcessor 'org.immutables:value'

    testImplementation project(':testing-utils')
    testImplementation 'org.junit.jupiter:junit-jupiter-params'
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.assertj:assertj-core'
    testImplementation 'org.mockito:mockito-core'
    testImplementation 'org.mockito:mockito-junit-jupiter'
    testImplementation 'io.github.origin-energy:java-snapshot-testing-junit5'
}

distributions {
    main {
        distributionBaseName = project.name
    }
}

docker {
    name "fern-java:${project.version}"
    tags 'latest', "${project.version}"
    files tasks.distTar.outputs, './init.sh', './gradle-cache-project/build.gradle'
    buildArgs([BUILD_VERSION: "${project.version}"])
}

task dockerTagVersion {
    dependsOn ":cli:dockerTag${project.version}"
}

testSets {
    eteTest
}

eteTest {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

task deleteSnapshot(type: Exec) {
    commandLine 'rm', '-rf', 'src/eteTest/java/com/fern/java/client/cli/__snapshots__/CliEteTest.snap'
}

rootProject.tasks.build.dependsOn eteTest
tasks.eteTest.dependsOn ':cli:dockerTagLatest'


